apply plugin: 'com.android.application'

android {
    compileSdk 35
    defaultConfig {
        applicationId "com.here.unittesting"
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    namespace 'com.here.unittesting'
}

// Use the mock JAR when executing unit tests and the real HERE SDK AARs when building the app.
def getHereSdkArtefactNames() {
    def aarFiles = fileTree(dir: 'libs', include: ['*.aar'])
    def artefactNames = []
    aarFiles.each { file ->
        // Filename without extension is necessary.
        artefactNames.add(org.apache.commons.io.FilenameUtils.getBaseName(file.name))
    }
    return artefactNames
}

// Exclude HERE SDK's AARs from unit test's dependencies.
configurations.testImplementation {
    getHereSdkArtefactNames().each { artefactName ->
        exclude module: artefactName
    }
}

dependencies {
    // It's not possible to exclude AAR files from dependencies
    // when they were added using 'fileTree'
    // as Gradle does not provide such a mechanism. Instead, below we load HERE SDK's AARs
    // from a 'local' repository. The names can be hardcoded and are loaded with wildcards, for example:
    // 'heresdk-explore-4.4.6.0.2841.release' and 'heresdk-units-core-release-v1.0' are acceptable as file names.
    getHereSdkArtefactNames().each { artefactName ->
        implementation(name: artefactName, ext:'aar')
    }

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    // Keep in mind that the original HERE SDK AAR must be excluded from
    // the test dependency, see above.
    testImplementation fileTree(dir: 'libs', include: ['*mock*.jar'])
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.1.0'
}
