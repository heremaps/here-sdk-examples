apply plugin: 'com.android.application'

android {
    compileSdk 34
    defaultConfig {
        applicationId "com.here.unittesting"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    namespace 'com.here.unittesting'
}

// Use the mock JAR when executing unit tests and the real HERE SDK AAR when building the app.
def getHereSdkArtefactName() {
    def aarFile = fileTree(dir: 'libs', include: ['heresdk-*.aar']).first()
    // Filename without extension is necessary.
    return org.apache.commons.io.FilenameUtils.getBaseName(aarFile.name)
}

// Exclude HERE SDK's AAR from unit test's dependencies.
configurations.testImplementation {
    exclude module: getHereSdkArtefactName()
}

dependencies {
    // It's not possible to exclude an AAR file from dependencies
    // when it was added using 'fileTree'
    // as Gradle does not provide such a mechanism. Instead, below we load HERE SDK's AAR
    // from a 'local' repository. The name can be hardcoded and is loaded with wildcards, for example:
    // 'heresdk-explore-4.4.6.0.2841.release' is acceptable as file name.
    implementation(name: getHereSdkArtefactName(), ext:'aar')

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    // Keep in mind that the original HERE SDK AAR must be excluded from
    // the test dependency, see above.
    testImplementation fileTree(dir: 'libs', include: ['*mock*.jar'])
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.1.0'
}
